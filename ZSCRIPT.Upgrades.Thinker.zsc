class FAK_UpgradeThinker : Thinker
{
	override void Tick()
	{
		let plr = HDPlayerPawn(players[PlayerNumber].mo);
		if (!plr || !plr.player || plr.Health <= 0 || plr.incapacitated > 0) // [Ace] Incap check is just in case.
		{
			return;
		}

		let wpn = HDWeapon(plr.player.ReadyWeapon);
		if (wpn is 'HDRevolver')
		{
			let rev = HDRevolver(wpn);
			if (rev.WeaponStatus[0] & 64 && rev.CylinderOpen && plr.player.cmd.buttons & BT_RELOAD && !(plr.player.oldbuttons & BT_RELOAD))
			{
				int totalRounds = 0;
				for (int i = BUGS_CYL1; i <= BUGS_CYL6; ++i)
				{
					if (wpn.WeaponStatus[i] > 0)
					{
						totalRounds++;
					}
				}
				if (totalRounds == 0)
				{
					for (int i = 0; i < 6; ++i)
					{
						bool useNineMil = (plr.player.cmd.buttons & BT_FIREMODE || plr.CountInv("HDRevolverAmmo") == 0);
						if (useNineMil && plr.CountInv("HDPistolAmmo") == 0)
						{
							break;
						}
						class<Inventory> ammotype = useNineMil ? 'HDPistolAmmo'  :'HDRevolverAmmo';
						plr.A_TakeInventory(ammotype, 1, TIF_NOTAKEINFINITE);
						rev.WeaponStatus[BUGS_CYL1] = useNineMil ? BUGS_NINEMIL : BUGS_MASTERBALL;
						plr.A_StartSound("weapons/deinoload", 8, CHANF_OVERLAP);
						rev.RotateCylinder();
					}
				}
			}
		}
		else if (wpn is 'Hunter' && wpn.WeaponStatus[0] & 256 && wpn.WeaponStatus[HUNTS_TUBE] < wpn.WeaponStatus[HUNTS_TUBESIZE] && wpn.WeaponStatus[SHOTS_SIDESADDLE] > 0)
		{
			wpn.WeaponStatus[HUNTS_TUBE]++;
			wpn.WeaponStatus[SHOTS_SIDESADDLE]--;
		}
		else if (wpn is 'ThunderBuster')
		{
			if (wpn.WeaponStatus[TBS_FLAGS] & 128)
			{
				wpn.DrainHeat(TBS_HEAT, 12);
			}
			if (wpn.WeaponStatus[TBS_FLAGS] & 256 && wpn.WeaponStatus[TBS_FLAGS] & TBF_ALT)
			{
				if (wpn.WeaponStatus[TBS_BATTERY] == -1)
				{
					LastBatteryCharge = -1;
				}
				else if (LastBatteryCharge == -1 && wpn.WeaponStatus[TBS_BATTERY] > -1)
				{
					LastBatteryCharge = wpn.WeaponStatus[TBS_BATTERY];
				}
				else if (wpn.WeaponStatus[TBS_BATTERY] < LastBatteryCharge)
				{
					if (!random(0, 2))
					{
						wpn.WeaponStatus[TBS_BATTERY]++;
					}
					LastBatteryCharge = wpn.WeaponStatus[TBS_BATTERY];
				}
			}
			if (wpn.WeaponStatus[TBS_FLAGS] & 512 && !(wpn.WeaponStatus[TBS_FLAGS] & TBF_ALT) && plr.player.GetPSprite(PSP_WEAPON).CurState == wpn.FindState('Shoot'))
			{
				ThunderBuster.ThunderZap(plr, HDWeapon.GetShootOffset(plr, wpn.barrellength, wpn.barrellength - HDCONST_SHOULDERTORADIUS), false, wpn.WeaponStatus[TBS_BATTERY]);
			}
		}
	}

	// [Ace] The reference may go null, but the number stays the same.
	int PlayerNumber;

	private int LastBatteryCharge;
}