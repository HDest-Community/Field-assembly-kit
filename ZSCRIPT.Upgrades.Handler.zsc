extend class FAK_Handler
{
	override void WorldThingSpawned(WorldEvent e)
	{
		if (e.Thing is 'RocketGrenade') // [Ace] Rocket Launcher & Blooper.
		{
			let rckt = RocketGrenade(e.Thing);
			if (rckt.master && rckt.master.player && rckt.master.player.ReadyWeapon)
			{
				let wpn = HDWeapon(rckt.master.player.ReadyWeapon);
				if (wpn is 'HDRL' && wpn.WeaponStatus[RLS_STATUS] & 32 || wpn is 'Blooper' && wpn.WeaponStatus[BLOPS_STATUS] & 8)
				{
					rckt.primed = true;
				}
			}
		}
		else if (e.Thing is 'BeamSpot') // [Ace] Thunderbuster.
		{
			let spot = BeamSpot(e.Thing);
			if (spot.target && spot.target.player && spot.target.player.ReadyWeapon && !spot.bSTANDSTILL) // [Ace] The flag is to avoid recursion. It's completely arbitrary.
			{
				let wpn = HDWeapon(spot.target.player.ReadyWeapon);
				if (wpn is 'ThunderBuster' && !(wpn.WeaponStatus[TBS_FLAGS] & TBF_ALT) && wpn.WeaponStatus[TBS_FLAGS] & 512)
				{
					for (int i = 0; i < 2; ++i)
					{
						Actor newSpot = Actor.Spawn("BeamSpot", spot.pos, ALLOW_REPLACE);
						newSpot.target = spot.target;
						newSpot.stamina = spot.stamina;
						newSpot.angle = spot.angle;
						newSpot.pitch = spot.pitch;
						newSpot.bSTANDSTILL = true;
					}
				}
			}
		}
		else if (e.Thing is 'HDSpent7mm')
		{
			for (int i = 0; i < MAXPLAYERS; ++i)
			{
				let plr = players[i].mo;
				if (!plr)
				{
					continue;
				}

				if (plr.player.ReadyWeapon is 'LiberatorRifle' && LiberatorRifle(plr.player.ReadyWeapon).WeaponStatus[LIBS_FLAGS] & 16384 && e.Thing.Distance2D(plr) < plr.radius * 2)
				{
					let pkp = HDUPK(e.Thing);
					pkp.picktarget = plr;
					pkp.pickupmessage = "";
					pkp.A_HDUPKGive();
					break;
				}
			}
		}
	}
}
