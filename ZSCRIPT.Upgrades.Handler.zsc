extend class FAK_Handler
{
	override void WorldThingSpawned(WorldEvent e)
	{
		// [Ace] Rocket Launcher & Blooper upgrades.
		if (e.Thing is 'GyroGrenade')
		{
			let gyro = GyroGrenade(e.Thing);
			if (gyro.master && gyro.master.player && gyro.master.player.ReadyWeapon)
			{
				let wpn = HDWeapon(gyro.master.player.ReadyWeapon);
				if (wpn is 'HDRL' && wpn.WeaponStatus[RLS_STATUS] & 32 || wpn is 'Blooper' && wpn.WeaponStatus[BLOPS_STATUS] & 8)
				{
					gyro.primed = true;
				}
			}
		}

		// [Ace] Thunderbuster upgrades.
		else if (e.Thing is 'BeamSpot')
		{
			let spot = BeamSpot(e.Thing);
			if (spot.target && spot.target.player && spot.target.player.ReadyWeapon && !spot.bSTANDSTILL) // [Ace] The flag is to avoid recursion. It's completely arbitrary.
			{
				let wpn = HDWeapon(spot.target.player.ReadyWeapon);
				if (wpn is 'ThunderBuster' && !(wpn.WeaponStatus[TBS_FLAGS] & TBF_ALT) && wpn.WeaponStatus[TBS_FLAGS] & 512)
				{
					for (int i = 0; i < 2; ++i)
					{
						Actor newSpot = Actor.Spawn("BeamSpot", spot.pos, ALLOW_REPLACE);
						newSpot.target = spot.target;
						newSpot.stamina = spot.stamina;
						newSpot.angle = spot.angle;
						newSpot.pitch = spot.pitch;
						newSpot.bSTANDSTILL = true;
					}
				}
			}
		}
	}
}